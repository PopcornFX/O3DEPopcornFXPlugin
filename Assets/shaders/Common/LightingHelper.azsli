//----------------------------------------------------------------------------
// Copyright Persistant Studios, SARL. All Rights Reserved.
// https://www.popcornfx.com/terms-and-conditions/
//----------------------------------------------------------------------------

#pragma once

#include <Atom/Features/Pipeline/Forward/ForwardPassOutput.azsli>
#include <Atom/Features/PBR/AlphaUtils.azsli>
#include <Atom/Features/PBR/LightingOptions.azsli>

#include "PopcornOptions.azsli"

#if PK_O3DE_SRG_ALL
#include <viewsrg_all.srgi>
#include <scenesrg_all.srgi>
#else
#include <viewsrg.srgi>
#include <scenesrg.srgi>
#endif

#include <Atom/RPI/Math.azsli>
#include <Atom/RPI/TangentSpace.azsli>

#include <Atom/Features/PBR/DefaultObjectSrg.azsli>
#include <Atom/Features/Pipeline/Forward/ForwardPassSrg.azsli>

#include <Atom/Features/PBR/Lighting/StandardLighting.azsli>
#include <Atom/Features/PBR/Lights/IblForward.azsli>
#include <Atom/Features/PBR/Decals.azsli>
#include <Atom/Features/ColorManagement/TransformColor.azsli>

// Helper function to compute forward lighting output for particles:
ForwardPassOutput 	ComputeParticleLighting(float3 worldPosition,
											float4 position,
											float3 vertexNormal,
											float3 normal,
											float3 tangent,
											float3 bitangent,
											float4 diffuseColor,
											float3 emissiveColor)
{
    // Metalness and roughness:
    const float metallic = MaterialSrg::m_metalness;
    const float roughness = MaterialSrg::m_roughness;

    // Diffuse color:
    const float3 baseColor = diffuseColor.rgb;
    float alpha = diffuseColor.a;

    // Emissive color:
    const float3 emissive = emissiveColor;

    const float occlusion = 1;
    const float specularF0Factor = 0.5f; // 0-1 range, will multiply an hardcoded 0.08f (BasePbrSurfaceData.azsli)
    const float clearCoatFactor = 0.0;
    const float clearCoatRoughness = 0.0;
    const float3 clearCoatNormal =  normal;
    const float2 anisotropy = 0.0;

    Surface surface;
    surface.position = worldPosition;

    // ------- Normal -------

    surface.vertexNormal = vertexNormal;
    surface.normal = normal;

    // ------- Specular -------

    float specularF0 = specularF0Factor;
    
    surface.SetAlbedoAndSpecularF0(baseColor, specularF0, metallic);

    // ------- Roughness -------

    surface.roughnessLinear = roughness;
    surface.CalculateRoughnessA();

    // ------- Transmission -------
#if ENABLE_TRANSMISSION
    surface.transmission.tint = float3(0,0,0);
    surface.transmission.thickness = 0.0f;
    surface.transmission.transmissionParams = float4(0,0,0,0);
#endif

   // ------- Lighting Data -------

#if PK_O3DE_MULTI_VIEW_SPECULAR
    float3 views[MAX_SHADING_VIEWS];
    views[0] = ViewSrg::m_worldPosition.xyz;    // Assume one view for forward pass for now
#endif

    LightingData lightingData;

#if PK_O3DE_LIT_MULTI_VIEW
#    if PK_O3DE_MULTI_VIEW_SPECULAR
    lightingData.Init(surface.position, surface.normal, surface.roughnessLinear, views);
#    else
    lightingData.Init(surface.position, surface.normal, surface.roughnessLinear, ViewSrg::m_worldPosition.xyz);
#    endif
#else
    lightingData.Init(surface.position, surface.normal, surface.roughnessLinear);
#endif

    // Light iterator
#if PK_O3DE_SEPARATE_LIGHTING
    LightCullingTileIterator tileIterator;
    tileIterator.Init(position, PassSrg::m_lightListRemapped, PassSrg::m_tileLightData);
#else
    lightingData.tileIterator.Init(position, PassSrg::m_lightListRemapped, PassSrg::m_tileLightData);
#endif

    // ------- Emissive -------

    lightingData.emissiveLighting = emissive;
    
    // ------- Occlusion -------

    lightingData.diffuseAmbientOcclusion = occlusion;
    lightingData.specularOcclusion = occlusion;


    // ------- Clearcoat -------

    surface.clearCoat.factor = 0.0f;
    surface.clearCoat.roughness = 0.0f;
    surface.clearCoat.normal = float3(0, 0, 0);
    
    // Diffuse and Specular response (used in IBL calculations)
#if PK_O3DE_MULTI_VIEW_SPECULAR
    lightingData.specularResponse = FresnelSchlickWithRoughness(lightingData.GetSpecularNdotV(), surface.specularF0, surface.roughnessLinear);
#else
    lightingData.specularResponse = FresnelSchlickWithRoughness(lightingData.NdotV, surface.specularF0, surface.roughnessLinear);
#endif
    lightingData.diffuseResponse = 1.0 - lightingData.specularResponse;

    // Multiscatter compensation factor
    lightingData.CalculateMultiscatterCompensation(surface.specularF0, o_specularF0_enableMultiScatterCompensation);

    // ------- Lighting Calculation -------

    // Apply Decals
#if PK_O3DE_SEPARATE_LIGHTING
    ApplyDecals(tileIterator, surface);
#else
    ApplyDecals(lightingData.tileIterator, surface);
#endif

    // Apply Direct Lighting
#if PK_O3DE_SEPARATE_LIGHTING
    ApplyDirectLighting(surface, lightingData, position, tileIterator);
#else
    ApplyDirectLighting(surface, lightingData, position);
#endif

    // Apply Image Based Lighting (IBL)
    ApplyIblForward(surface, lightingData);

    // Finalize Lighting
    lightingData.FinalizeLighting();

    if (o_opacity_mode == OpacityMode::Blended || o_opacity_mode == OpacityMode::TintedTransparent)
    {
#if PK_O3DE_MULTI_VIEW_SPECULAR
        alpha = FresnelSchlickWithRoughness(lightingData.GetSpecularNdotV(), alpha, surface.roughnessLinear).x; // Increase opacity at grazing angles.
#else
        alpha = FresnelSchlickWithRoughness(lightingData.NdotV, alpha, surface.roughnessLinear).x; // Increase opacity at grazing angles.
#endif
    }

    PbrLightingOutput lightingOutput = GetPbrLightingOutput(surface, lightingData, alpha);

    lightingOutput.m_diffuseColor.rgb *= lightingOutput.m_diffuseColor.w; // pre-multiply diffuse
    lightingOutput.m_diffuseColor.rgb += lightingOutput.m_specularColor.rgb; // add specular
    lightingOutput.m_diffuseColor.rgb += lightingData.emissiveLighting.rgb; // add emissive

    ForwardPassOutput fwdOut;

    fwdOut.m_diffuseColor = lightingOutput.m_diffuseColor;
    fwdOut.m_specularColor = lightingOutput.m_specularColor;
    fwdOut.m_specularF0 = lightingOutput.m_specularF0;
    fwdOut.m_albedo = lightingOutput.m_albedo;
    fwdOut.m_normal = lightingOutput.m_normal;

/*
#ifdef FORCE_IBL_IN_FORWARD_PASS
    bool useDiffuseIbl = true;
    bool useSpecularIbl = true;
    bool useIbl = o_enableIBL;
#else
    bool isTransparent = (o_opacity_mode == OpacityMode::Blended || o_opacity_mode == OpacityMode::TintedTransparent);
    bool useDiffuseIbl = isTransparent;
    bool useSpecularIbl = (isTransparent || o_meshUseForwardPassIBLSpecular || o_materialUseForwardPassIBLSpecular);
    bool useIbl = o_enableIBL && (useDiffuseIbl || useSpecularIbl);
#endif

    if(useIbl)
    {
        float iblExposureFactor = pow(2.0, SceneSrg::m_iblExposure);
        if(useDiffuseIbl)
        {
          fwdOut.m_diffuseColor = float4(1,1,1,1);
          fwdOut.m_diffuseColor.rgb =  surface.albedo*iblExposureFactor * SceneSrg::m_diffuseEnvMap.Sample(SceneSrg::m_samplerEnv, GetCubemapCoords(normal)).rgb * lightingData.diffuseAmbientOcclusion;
        }

    }
*/
    return fwdOut;
}
